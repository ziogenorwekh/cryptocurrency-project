plugins {
    id 'java'
    id 'org.springframework.boot' version "${springBootPluginVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
    id 'com.google.cloud.tools.jib' version "${jibPluginVersion}"
}
group = 'shop.shportfolio'
version = '1.0-SNAPSHOT'



dependencies {
    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation project(':coupon-service:coupon-domain')
    implementation project(':coupon-service:coupon-application')
    implementation project(':coupon-service:coupon-api')
    implementation project(':coupon-service:coupon-infrastructure')
    implementation project(':common')

    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation "io.micrometer:micrometer-registry-prometheus"
    implementation("io.micrometer:micrometer-tracing-bridge-otel")
    implementation("io.opentelemetry:opentelemetry-exporter-zipkin")
    implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client"
    implementation "org.springframework.cloud:spring-cloud-starter-config"
    implementation "org.springframework.cloud:spring-cloud-config-client"

}
test {
    useJUnitPlatform()
}
jar {
    enabled = false // 표준 jar는 끈다
}

bootJar {
    enabled = true
    archiveFileName = 'coupon-service.jar'
    manifest {
        attributes 'Main-Class': 'shop.shportfolio.coupon.container.CouponApplication'
    }
}


jib {
    from {
        image = 'bellsoft/liberica-openjre-alpine:17.0.15'
        platforms {
            platform {
                architecture = 'arm64'
                os = 'linux'
            }
        }
    }
    to {
        image = 'coupon-service'
        tags = ['latest']
    }
    container {
        mainClass = 'shop.shportfolio.coupon.container.CouponApplication' // 메인 클래스 지정
        jvmFlags = ['-Dspring.profiles.active=dev']
        ports = ['20020'] // 컨테이너에서 노출할 포트
        creationTime = 'USE_CURRENT_TIMESTAMP'

    }
}
